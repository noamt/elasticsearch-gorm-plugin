buildscript {
    ext {
        grailsVersion = project.grailsVersion
        gormVersion = project.gormVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.ajoberstar:gradle-git:1.4.2'
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

version "2.4.0.RC1"
group "org.grails.plugins"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"

ext {
    gradleWrapperVersion = project.gradleWrapperVersion
    elasticsearchVersion = '5.4.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

jar {
    exclude "test/**"
    exclude "test/**/**"
}

groovydoc {
    exclude "test/**"
    exclude "test/**/**"
}

sourcesJar {
    exclude "test/**"
    exclude "test/**/**"
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"

    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"

    compile "org.grails:grails-core"
    compile "org.grails.plugins:hibernate5"
    compile "org.grails.plugins:cache"
    compile "org.hibernate:hibernate-ehcache"

    compile "org.apache.logging.log4j:log4j-api:2.7"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'

    compile "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
    compile group: 'org.elasticsearch.client', name: 'transport', version: elasticsearchVersion

    console "org.grails:grails-console"

    profile "org.grails.profiles:web-plugin"

    testRuntime 'com.spatial4j:spatial4j:0.4.1'
    testRuntime 'org.apache.tomcat:tomcat-jdbc'

    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails:grails-test-mixins:3.3.0"

    testCompile 'com.vividsolutions:jts:1.13'
    testCompile 'com.googlecode.json-simple:json-simple:1.1.1'

    compile 'org.elasticsearch.plugin:mapper-attachments:2.4.6'
}

task cleanLibsDir(type: Delete) {
    delete fileTree(dir: 'libs')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

check.doLast {
    cleanDataDir.execute()
}

clean.doLast {
    cleanDataDir.execute()
    cleanLibsDir.execute()
}

task cleanDataDir(type: Delete) {
    delete fileTree(dir: 'data')
}

apply from: "gradle/documentation.gradle"

apply from: "gradle/publish.gradle"




