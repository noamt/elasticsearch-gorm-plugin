=== Quick Start

* Install Elasticsearch using instructions from https://www.elastic.co/guide/en/elasticsearch/reference/5.4/gs-installation.html[here].

* Just like any other Grails plugin, through the Grails Plugin center and Edit your project's +build.gradle+ file, by adding the plugin's dependency declaration:

----
dependencies {
    ...
    compile "org.grails.plugins:elasticsearch:2.4.0"
    ...
}
----

* Overriding Spring Boot elasticsearch version

Spring Boot 1.3.x supports Elasticsearch 1.5.2 OOTB (https://github.com/spring-projects/spring-boot/blob/master/spring-boot-dependencies/pom.xml#L76) and will install dependencies for this version, if not explicitly overriden. To do so add the following to your `build.gradle`:

[source, groovy]
----
def elasticsearchVersion = '2.3.3'
ext['elasticsearch.version'] = elasticsearchVersion
----


* Updated `application.yml` with default configurations:
----
elasticSearch:
    datastoreImpl: hibernateDatastore
    client:
        mode: transport
        hosts:
          - {host: localhost, port: 9300}
    cluster.name: <ENTER CLUSTER NAME HERE>
----

In order debug Elasticsearch plugin, add `logger("grails.plugins.elasticsearch", DEBUG, ['STDOUT'], false)` in `logback.groovy file as following:

----
import grails.util.BuildSettings
import grails.util.Environment
import org.springframework.boot.logging.logback.ColorConverter
import org.springframework.boot.logging.logback.WhitespaceThrowableProxyConverter

import java.nio.charset.Charset

conversionRule 'clr', ColorConverter
conversionRule 'wex', WhitespaceThrowableProxyConverter

// See http://logback.qos.ch/manual/groovy.html for details on configuration
appender('STDOUT', ConsoleAppender) {
    encoder(PatternLayoutEncoder) {
        charset = Charset.forName('UTF-8')

        pattern =
                '%clr(%d{yyyy-MM-dd HH:mm:ss.SSS}){faint} ' + // Date
                        '%clr(%5p) ' + // Log level
                        '%clr(---){faint} %clr([%15.15t]){faint} ' + // Thread
                        '%clr(%-40.40logger{39}){cyan} %clr(:){faint} ' + // Logger
                        '%m%n%wex' // Message
    }
}

def targetDir = BuildSettings.TARGET_DIR
if (Environment.isDevelopmentMode() && targetDir != null) {
    appender("FULL_STACKTRACE", FileAppender) {
        file = "${targetDir}/stacktrace.log"
        append = true
        encoder(PatternLayoutEncoder) {
            pattern = "%level %logger - %msg%n"
        }
    }
    logger("StackTrace", ERROR, ['FULL_STACKTRACE'], false)
    logger("grails.plugins.elasticsearch", DEBUG, ['STDOUT'], false)
    root(ERROR, ['STDOUT', 'FULL_STACKTRACE'])
}
else {
    root(ERROR, ['STDOUT'])
}
----

==== Default mapping

To declare a domain class to be searchable, the simplest way is to define the following static property in the code:

[source,groovy]
----
static searchable = true
----

The plugin will generate a default mapping for each properties of the domain.

==== Custom mapping

You can customize how each properties are mapped to the index using a closure. The syntax is similar to GORM's mapping DSL.

[source,groovy]
----
static searchable = {
    // mapping DSL...
}
----

See below for more details on the mapping DSL.

==== Limit properties with only/except
`only` and `except` are used to limit the properties that are made searchable.
You may not define both except & only settings at the same time.

The following code will only map the 'message' property, any others will be ignored.

[source, groovy]
----
class Tweet {
    static searchable = {
        only = 'message'
    }
    String message
    String someUselessField
}
----

The following code will map all properties except the one specified.

[source, groovy]
----
class Tweet {
    static searchable = {
        except = 'someUselessField'
    }
    String message
    String someUselessField
}
----

You can use a Collection to specify several properties.

[source, groovy]
----
class Tweet {
    static searchable = {
        except = ['someUselessField', 'userName']
    }
    String message
    String userName
    String someUselessField
}
----

[NOTE]
====
The properties that are ignored will not be sent to ElasticSearch. It also means that when you will get back a domain
from ElasticSearch, some fields that are not supposed to be null, may still be null.
====

==== Including transients

How the plugin manages transient properties is controlled by the `elasticSearch.includeTransients` configuration property. If this is set to `false` only transient properties explicitly included in `only` will be mapped and searchable, if set to `true`, all domain class properties will be mapped, including `transients`.

The following are valid examples

[source, groovy]
----
//assert grailsApplication.config.elasticSearch.includeTransients == false
class Person {
    String firstName
    String lastName
    String getFullName() {
        firstName + " " + lastName
    }
    static transients = ['fullName']
    static searchable = {
        only = ['fullName']
    }
}

// new Person(firstNameme: "Nikola", lastName: "Tesla")
// can be found using:
// def tesla = Person.search("Nikola Tesla").searchResults.first()
----

[source, groovy]
----
//assert grailsApplication.config.elasticSearch.includeTransients == true
class Multiplication {
    int opA
    int opB
    int getResult() {
        opA * opB
    }
    static transients = ['result']
    static searchable = true
}
// new Multiplication(opA: 2, opB: 3)
// can be found using:
// def multiplication = Multiplication.search("2").searchResults.first()
// def multiplication = Multiplication.search("3").searchResults.first()
// def multiplication = Multiplication.search("6").searchResults.first()
----

[NOTE]
====
From the examples above, once the domain object is found, its transient values will be calculated from the information stored on ElasticSearch: `multiplication.result == 6`, but `tesla.fullName == "null null"`, as firstName and lastName where not indexed. This behaviour can be prevented by creating convenient setters for the transient properties.
====

==== Transients and collections

When transient properties are collections the only way the plugin can define the correct ElasticSearch mapping during boot is if the element types are explicitly defined on the grails domain object. For instances of `Collection` this can be achieved by defining its type on the `hasMany` property (otherwise the ElasticSearch type will be defined as `object`). This is not required for arrays.

Some valid examples:

[source, groovy]
----
class Tweet {
    String message
    List getHashtags() { ... }
    static transients = ['hashtags']
    static hasMany = [hashtags: String]
    static searchable = {only = 'hashtags' }
}
----

[source, groovy]
----
class FamilyGuy {
    String wife
    String son
    String daughter
    String baby
    String[] getRelatives() { ... }
    static transients = ['relatives']
    static searchable = { only = 'relatives' }
}
----
